// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import "./CreatorRegistry.sol";
import "./DeviceRegistry.sol";
import {License} from "./Licensing.sol";
import {Asset, Transformation} from "./Utils.sol";

/**
 * @dev Interface for the SNARK verifier.
 * This interface is used to verify proofs generated by the SNARK.
 */
interface ISnarkVerifier {
    function verifyOpaqueNovaProofWithInputs(
        uint256 steps,
        uint256[2] calldata initial_state,
        uint256[2] calldata final_state,
        uint256[25] calldata proof
    ) external view returns (bool);
}

/**
 * @title AssetGateway
 * @dev Main entry point for registering assets in the marketplace.
 */
contract AssetGateway {
    CreatorRegistry public creatorRegistry;
    DeviceRegistry public deviceRegistry;
    mapping(Transformation => ISnarkVerifier) public verifiers;

    uint256 public assetCount;

    // Mapping from asset ID to Asset details.
    mapping(uint256 => Asset) public assets;

    event NewAssetRegistered(
        uint256 indexed assetId,
        address indexed creator,
        uint256 imageHash,
        uint256 captureTime,
        address device,
        License license,
        uint256 timestamp
    );

    event EditedAssetRegistered(
        uint256 indexed assetId,
        address indexed creator,
        uint256 imageHash,
        uint256 sourceAssetId,
        Transformation transformation,
        License license,
        uint256 timestamp
    );

    /**
     * @notice Constructor initializes the contract with the CreatorRegistry and DeviceRegistry.
     * @param _creatorRegistry Address of the deployed CreatorRegistry contract.
     * @param _deviceRegistry Address of the deployed DeviceRegistry contract.
     */
    constructor(address _creatorRegistry, address _deviceRegistry, ISnarkVerifier[8] memory _verifiers) {
        creatorRegistry = CreatorRegistry(_creatorRegistry);
        deviceRegistry = DeviceRegistry(_deviceRegistry);

        verifiers[Transformation.Blur] = _verifiers[0];
        verifiers[Transformation.Brightness] = _verifiers[1];
        verifiers[Transformation.Contrast] = _verifiers[2];
        verifiers[Transformation.Crop] = _verifiers[3];
        verifiers[Transformation.Grayscale] = _verifiers[4];
        verifiers[Transformation.Redact] = _verifiers[5];
        verifiers[Transformation.Resize] = _verifiers[6];
        verifiers[Transformation.Sharpness] = _verifiers[7];
    }

    /**
     * @notice Registers a new original asset captured by a verified device.
     * @param imageHash The uint256 hash of the original image.
     * @param captureTime Unix timestamp when the image was captured.
     * @param license The licensing details for the asset.
     * @param deviceId The address of the device that captured the image.
     * @param deviceSignature The deviceâ€™s signature over (creator, imageHash, captureTime).
     */
    function registerNewAsset(
        uint256 imageHash,
        uint256 captureTime,
        License license,
        address deviceId,
        bytes calldata deviceSignature
    ) external {
        // 1. Ensure the creator is verified.
        address creator = msg.sender;
        require(creatorRegistry.verifyCreator(creator), "Creator not verified");

        // 2. Create a message hash for device signature verification and vaildate it.
        bytes32 messageHash = keccak256(abi.encodePacked(creator, imageHash, captureTime));
        require(
            deviceRegistry.verifyDeviceSignature(messageHash, deviceSignature, deviceId),
            "Invalid device signature"
        );

        // 3. Increment asset count and store the original asset.
        assetCount++;
        assets[assetCount] = Asset({
            creator: creator,
            imageHash: imageHash,
            captureTime: captureTime,
            license: license,
            timestamp: block.timestamp,
            sourceAssetId: 0,
            transformation: Transformation.NoTransformation
        });

        emit NewAssetRegistered(
            assetCount,
            creator,
            imageHash,
            captureTime,
            deviceId,
            license,
            block.timestamp
        );
    }

    /**
     * @notice Registers an edited asset based on a source asset.
     * @param editedImageHash The uint256 hash of the edited image.
     * @param sourceAssetId The ID of the original asset being edited.
     * @param transformation The transformation applied to the original asset.
     * @param license The licensing details for the edited asset.
     */
    function registerEditedAsset(
        uint256 editedImageHash,
        uint256 sourceAssetId,
        Transformation transformation,
//        uint256[25] calldata proof,
        License license
    ) external {
        // 1. Ensure the creator is verified.
        address creator = msg.sender;
        require(creatorRegistry.verifyCreator(creator), "Creator not verified");

        // 2. Ensure the source asset exists.
        require(sourceAssetId > 0 && sourceAssetId <= assetCount, "Source asset does not exist");

        // 3. Ensure the transformation is valid.
        require(transformation != Transformation.NoTransformation, "Invalid transformation");
        // TODO

        // 3. Increment asset count and store the asset.
        assetCount++;
        assets[assetCount] = Asset({
            creator: creator,
            imageHash: editedImageHash,
            captureTime: assets[sourceAssetId].captureTime,
            license: license,
            timestamp: block.timestamp,
            sourceAssetId: sourceAssetId,
            transformation: transformation
        });

        emit EditedAssetRegistered(
            assetCount,
            creator,
            editedImageHash,
            sourceAssetId,
            transformation,
            license,
            block.timestamp
        );
    }

    /**
     * @notice Retrieves details for a given asset by its ID.
     * @param assetId The asset's unique ID.
     * @return The asset's full details.
     */
    function getAsset(uint256 assetId) external view returns (Asset memory) {
        require(assetId > 0 && assetId <= assetCount, "Asset does not exist");
        return assets[assetId];
    }
}
